pkgname=rust
pkgver=1.63.0
pkgrel=1
conflicts=(rust-nightly)
pkgdesc="Systems programming language focused on safety, speed and concurrency"
url=https://www.rust-lang.org/
arch=(x86_64)
license=(MIT Apache)
options=(!emptydirs !strip)
llvm_ver=13.0.1
depends=(gcc llvm curl gcc)
makedepends=(clang rust)
source=(
  "https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.xz"
  "https://github.com/llvm/llvm-project/releases/download/llvmorg-$llvm_ver/compiler-rt-$llvm_ver.src.tar.xz"
  0001-cargo-Change-libexec-dir.patch
  0001-bootstrap-Change-libexec-dir.patch
  0002-compiler-Change-LLVM-targets.patch
  0003-compiler-Use-wasm-ld-for-wasm-targets.patch
  0004-remove-include-linux-fs.h.patch
  config-drunk.toml
)

prepare() {
  cd rustc-$pkgver-src

  # Patch bootstrap and cargo so credential helpers
  # are in /usr/lib instead of /usr/libexec
  patch -d src/tools/cargo -Np1 < $srcdir/0001-cargo-Change-libexec-dir.patch
  patch -Np1 -i $srcdir/0001-bootstrap-Change-libexec-dir.patch

  # Use our *-pc-linux-gnu targets, making LTO with clang simpler
  patch -Np1 -i $srcdir/0002-compiler-Change-LLVM-targets.patch

  # Use our wasm-ld
  patch -Np1 -i $srcdir/0003-compiler-Use-wasm-ld-for-wasm-targets.patch

  # https://github.com/llvm/llvm-project/issues/56421
  patch -Np1 -d src/llvm-project < $srcdir/0004-remove-include-linux-fs.h.patch
}

build() {
  cd rustc-$pkgver-src

  cp -f $srcdir/config-drunk.toml config.toml

  export RUST_BACKTRACE=1
  export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$llvm_ver.src"
  [[ -d $RUST_COMPILER_RT_ROOT ]]

  DESTDIR="$srcdir/dest-rust" python ./x.py install -j "$(nproc)"

  cd ../dest-rust

  # delete unnecessary files, e.g. files only used for the uninstall script
  rm usr/lib/rustlib/{components,install.log,rust-installer-version,uninstall.sh}
  rm usr/lib/rustlib/manifest-*

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft usr/lib   usr/lib/rustlib/x86_64-unknown-linux-gnu/lib/*.so

  mkdir -p usr/share/bash-completion
  mv etc/bash_completion.d usr/share/bash-completion/completions

  mkdir -p usr/share/licenses/rust
  mv -t usr/share/licenses/rust usr/share/doc/rust/{COPYRIGHT,LICENSE*}
}

package() {
  cp -a dest-rust/* $pkgdir
}
