pkgname=pipewire
pkgver=0.3.60
pkgrel=1
arch=(x86_64)
depends=(pulseaudio alsa-lib libusb gst-plugins-base dbus sdl2 ncurses
	libcanberra libsndfile bluez webrtc-audio-processing libx11 libxfixes jack2
	libfdk-aac gst-plugin-pipewire)
makedepends=(git meson doxygen python3 valgrind)
source=(https://gitlab.freedesktop.org/pipewire/pipewire/-/archive/$pkgver/pipewire-$pkgver.tar.gz)
install=pipewire.install

prepare() {
	cd $pkgname-$pkgver

	# remove export of LD_LIBRARY_PATH for pw-jack as it would add /usr/lib
	sed -i '/LD_LIBRARY_PATH/d' pipewire-jack/src/pw-jack.in
}

build() {
	cd $pkgname-$pkgver

	meson build \
	--prefix=/usr \
	-D docs=enabled \
	-D jack=enabled \
	-D jack-devel=true \
	-D libjack-path=/usr/lib \
	-D libcamera=disabled \
	-D udevrulesdir=/usr/lib/udev/rules.d \
	-D roc=disabled \
	-D session-managers=[]

	meson compile -C build
}

package() {
	cd $pkgname-$pkgver

	meson install -C build --destdir "$pkgdir"

	rm -rf $pkgdir/usr/include/jack
	rm -rf $pkgdir/usr/lib/libjack*
	rm -rf $pkgdir/usr/lib/pkgconfig/jack.pc
}
