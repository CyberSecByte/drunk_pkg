pkgname=lib32-mesa
rpkgname=mesa
pkgver=22.1.3
pkgrel=3
arch=('x86_64')
depends=('glibc' 'lib32-libdrm' 'lib32-llvm' 'lib32-libglvnd' 'lib32-libx11' 'lib32-wayland'
	'lib32-libxml2' 'lib32-libva' 'lib32-expat' 'lib32-cairo' 'lib32-elfutils' 'lib32-libxshmfence'
	'lib32-libxxf86vm' 'lib32-vdpau' 'lib32-vulkan-icd-loader' 'lib32-lm-sensors' 'lib32-clang'
	'lib32-libomxil-bellagio' 'lib32-polly' 'lib32-libxrandr' 'lib32-libunwind' 'libclc')
source=(https://mesa.freedesktop.org/archive/mesa-$pkgver.tar.xz)
makedepends=('directx-headers' 'vulkan-headers' 'python3-pip' 'clang' 'cmake' 'byacc' 'bison' 'glslang' 'lib32-directx-headers' 'lib32-polly')

prepare() {
	cd mesa-$pkgver

	pip install setuptools
	echo "[!]: Installing mako with pip"
	pip install mako
	sed '1s/python/&3/' -i bin/symbols-check.py

	rm -rf build
}

build() {
	cd mesa-$pkgver

	mkdir -p build && cd build

	export CC="clang -m32"
	export CXX="clang++ -m32"
	export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

	cat >crossfile.ini <<END
[binaries]
llvm-config = '/usr/bin/llvm-config32'
END

	meson \
	--native-file crossfile.ini \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	--buildtype=release \
	-D b_lto=true \
	-D b_ndebug=true \
	-D platforms=x11,wayland \
	-D gallium-drivers=r300,r600,radeonsi,nouveau,virgl,svga,swrast,iris,crocus,zink,d3d12 \
	-D vulkan-drivers=amd,intel,swrast \
	-D vulkan-layers=device-select,intel-nullhw,overlay \
	-D dri3=enabled \
	-D egl=enabled \
	-D llvm=enabled \
	-D gallium-nine=true \
	-D gallium-omx=disabled \
	-D gallium-opencl=icd \
	-D gallium-va=enabled \
	-D gallium-vdpau=enabled \
	-D gallium-xa=enabled \
	-D gallium-xvmc=disabled \
	-D gbm=enabled \
	-D gles1=disabled \
	-D gles2=enabled \
	-D glvnd=true \
	-D glx=dri \
	-D libunwind=enabled \
	-D lmsensors=enabled \
	-D osmesa=true \
	-D shared-glapi=enabled \
	-D microsoft-clc=disabled \
	-D valgrind=disabled \
	..

	ninja
}

package() {
	cd mesa-$pkgver/build
	DESTDIR=$pkgdir ninja install

	rm -rf $pkgdir/etc
	rm -rf $pkgdir/usr/{bin,etc,share,include,sbin}
}
